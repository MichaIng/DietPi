ODROIDC2-UBOOT-CONFIG

# Auto-detection of monitor settings based on your screen information: "false" or "true"
# See: http://odroid.com/dokuwiki/doku.php?id=en:c2_auto_detect_display
setenv display_autodetect "true"

# Display modes
# 480 Lines (720x480)
# - "480i60hz" # Interlaced 60Hz
# - "480i_rpt" # Interlaced for Rear Projection Televisions 60Hz
# VESA modes
# - "640x480p60hz"
# - "800x480p60hz"
# - "480x320p60hz"
# - "480x800p60hz"
# - "800x600p60hz"
# - "1024x600p60hz"
# - "1024x768p60hz"
# - "1280x800p60hz"
# - "1280x1024p60hz"
# - "1360x768p60hz"
# - "1440x900p60hz"
# - "1600x900p60hz"
# - "1680x1050p60hz"
# - "1600x1200p60hz"
# - "1920x1200p60hz"
# - "2560x1080p60hz"
# - "2560x1440p60hz"
# - "2560x1600p60hz"
# - "3440x1440p60hz"
setenv m "720p60hz"

# Custom modeline!
# To use custom modeline you need to disable all the above resolutions.
# and setup your own! 
# For more information check our wiki: 
# http://odroid.com/dokuwiki/doku.php?id=en:c2_hdmi_autosetting
# Example:
#setenv m "custombuilt" 
#setenv modeline "1920,1200,154000,74040,60,1920,1968,2000,2080,1200,1202,1208,1235,1,0,1"

# HDMI BPP mode
setenv m_bpp "32"

# HDMI DVI/VGA mode: "dvi" or "vga"
#setenv vout "dvi"

# HDMI HotPlug Detection control: "false" or "true"
# Allows you to force HDMI thinking that the cable is connected.
# true = HDMI will believe that cable is always connected
# false = will let board/monitor negotiate the connection status
setenv hpd "true"

# Toggle HDMI output: "false" or "true"
setenv monitor_onoff "false"

# Toggle CEC support
# 0 = Disable CEC
# 1 = Enable CEC
# 2 = Enable but disable auto TV switching ON
setenv cec "1"

# Force HDMI to use RGB colorspace regardless of TV request
# 0 Disable
# 1 Enable
setenv hdmi_forcergb "0"

# Default console settings
setenv condev "consoleblank=0 console=tty0 console=ttyS0,115200n8"

# Meson Timer
# 1 - Meson Timer
# 0 - Arch Timer
# Using meson_timer improves the video playback however it breaks KVM (virtualization).
# Using arch timer allows KVM/Virtualization to work however you'll experience poor video
setenv mesontimer "1"

# Disable video output
# - Setting nographics to "1" will disable all video subsystems.
# - This mode is ideal for headless server type usage, as it saves ~300 MiB RAM.
#setenv nographics "1"

# Toggle UHS (Ultra High Speed) micro SD mode: "false" or "true"
setenv disableuhs "false"

# Force micro SD detection: "false" or "true"
# - Forces the MMC controlled to believe that a card is connected.
setenv mmc_removable "true"

# Toggle USB multi-webcam tweak: "false" or "true"
# - Only enable this if you use it.
setenv usbmulticam "false"

# Disable/Enable ODROID-VU7 Touchscreen: "false" or "true"
setenv disable_vu7 "false"

### WARNING!!! WARNING!!! WARNING!!!
# Before changing anything here please read the wiki entry:
# http://odroid.com/dokuwiki/doku.php?id=en:c2_set_cpu_freq

# Max CPU cores: "1", "2", "3" or "4"
setenv maxcpus "4"

# Max CPU frequency in MHz: "1536" (default), "1680", "1752", "1896", "1920", "1944", "2016"
setenv max_freq "1536"

### DO NOT EDIT ANYTHING BELOW THIS LINE ###

# Apply display auto-detect
if test "${display_autodetect}" = "true"; then usb pwren; hdmitx edid; fi

# Apply custom resolution mode
if test "${m}" = "custombuilt"; then setenv cmode "modeline=${modeline}"; fi

# Apply VU7 setting
if test "${disable_vu7}" = "false"; then setenv hid_quirks "usbhid.quirks=0x0eef:0x0005:0x0004"; fi

# Apply CEC option
if test "${cec}" = "1"; then setenv cec "cecf"; fi
if test "${cec}" = "2"; then setenv cec "cec7"; fi

# Apply forced RGB mode
if test "${hdmi_forcergb}" = "1"; then setenv forcergb "hdmitx=forcergb"; fi

# Boot args
setenv bootargs "root=/dev/mmcblk0p2 rootwait ro ${condev} no_console_suspend hdmitx=${cec} ${forcergb} hdmimode=${m} m_bpp=${m_bpp} vout=${vout} fsck.repair=yes max_freq=${max_freq} maxcpus=${maxcpus} monitor_onoff=${monitor_onoff} disableuhs=${disableuhs} mmc_removable=${mmc_removable} usbmulticam=${usbmulticam} ${hid_quirks} net.ifnames=0 elevator=noop disablehpd=${hpd} ${cmode} systemd.unified_cgroup_hierarchy=0"

# Booting

setenv loadaddr "0x11000000"
setenv dtb_loadaddr "0x1000000"
setenv initrd_loadaddr "0x13000000"

fatload mmc 0:1 ${initrd_loadaddr} uInitrd
fatload mmc 0:1 ${loadaddr} Image
fatload mmc 0:1 ${dtb_loadaddr} meson64_odroidc2.dtb
fdt addr ${dtb_loadaddr}

if test "${mesontimer}" = "0"; then fdt rm /meson_timer; fdt rm /cpus/cpu@0/timer; fdt rm /cpus/cpu@1/timer; fdt rm /cpus/cpu@2/timer; fdt rm /cpus/cpu@3/timer; fi
if test "${mesontimer}" = "1"; then fdt rm /timer; fi

if test "${nographics}" = "1"; then fdt rm /reserved-memory; fdt rm /aocec; fi
if test "${nographics}" = "1"; then fdt rm /meson-fb; fdt rm /amhdmitx; fdt rm /picdec; fdt rm /ppmgr; fi
if test "${nographics}" = "1"; then fdt rm /meson-vout; fdt rm /mesonstream; fdt rm /meson-fb; fi
if test "${nographics}" = "1"; then fdt rm /deinterlace; fdt rm /codec_mm; fi

if test "${cec}" = "0"; then fdt rm /aocec; fi

booti ${loadaddr} ${initrd_loadaddr} ${dtb_loadaddr}
